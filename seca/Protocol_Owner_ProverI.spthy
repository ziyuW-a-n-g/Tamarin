theory Owners_and_Provers
begin

builtins: asymmetric-encryption, signing
functions:
  
  hash/4, 
  hash2/2
/*
   Protocol between Owner and Prover
 */
    

// Public key infrastructure
rule Proveregister_pk:
  [ Fr(~ltkA) ]
  --[Register($A, ~ltkA)]->
  [ !Ltk($A, ~ltkA), !Pk($A, pk(~ltkA)), Out(pk(~ltkA)) ]

rule Provereveal_ltk:
  [ !Ltk(A, ltkA) ] --[ Revltk(A)    ]-> [ Out(ltkA) ]

rule Generate_shared_key:
[Fr(~k),
!Pk($Owner, pkOwner),
!Pk($Prover, pkProver)
]
--[Generatekey(pkOwner,pkProver,~k)]->
[!Ltksys($Owner,$Prover,pkOwner,pkProver,~k)]

rule Reveal_k:
      [!Ltksys($Owner,$Prover,pkOwner,pkProver,k)]
        --[Revk($Owner,$Prover)]->
      [Out(k)]

rule Owner_1:
  let m1 = <~time_I,~pid,sign(<~time_I,~pid>,ltk_Owner),sign(~time_I,ltk_Owner)>
  in
    [ Fr(~time_I),
    Fr(~pid),
    !Ltk($Owner, ltk_Owner),
     !Pk($Prover, pkProver)
    ]
  --[ 
  Step1(),
  Running($Owner, $Prover, <'param1',~pid,~time_I>)
    ]->
    [ Out( m1 ),
      St_Owner_1($Owner, $Prover, ~time_I,~pid)
    ]

rule Prover_1:
  let m1 = <~time_I,~pid,s1,s2>
      Ti = hash2(~Ni,~time_I)
      m2 = <~repi,Ti,hash(~k,~repi,Ti,~pid),sign(Ti,ltk_Prover)>
  in
    [ !Ltk($Prover, ltk_Prover),
      In( m1 ),
      !Ltksys($Owner,$Prover,pkOwner,pkProver,~k),
      Fr(~repi),
      Fr(~Ni)
    ]
  --[ Step2(),
      Eq(verify(s1,<~time_I,~pid>,pkOwner),true),
      Eq(verify(s2,~time_I,pkOwner),true),
      Commit($Prover, $Owner, <'param1',~time_I>),
      Running($Prover, $Owner, <'Ti',Ti>),
      Running($Prover, $Owner, <'pidi',~pid>),
       Running($Prover, $Owner, <'repi',~repi>)
    ]->
    [ Out( m2 ),
      St_Prover_1($Prover, $Owner, ~time_I, ~pid, ~Ni, ~repi)
    ]

rule Owner_2:
  let m2 = <~repi,Ti,hash(~k,~repi,Ti,~pid),s3>
  in
    [ St_Owner_1($Owner, $Prover, ~time_I,~pid),
      In( m2 ),
    !Ltksys($Owner,$Prover,pkOwner,pkProver,~k)
    ]
  --[ Step3(),
      Eq(verify(s3,Ti,pkProver),true),
      Commit ($Owner, $Prover, <'Ti',Ti>),
      Commit ($Owner, $Prover, <'pidi',~pid>),
      Commit ($Owner, $Prover, <'repi',~repi>) 
    ]->
    [ ]


restriction Equality:
"All x y #i. Eq(x,y) @i ==> x = y"

restriction register_once:
  " All ltk1 ltk2 A #i #j. Register(A, ltk1)@i & Register(A, ltk2)@j ==> (#i = #j & ltk1 = ltk2)"

restriction Generatekey_two_different_party:
  " All pkOwner pkProver k #i. Generatekey(pkOwner,pkProver,k)@i  ==> not (pkOwner=pkProver)"
restriction Generatekey_once:
  " All pkOwner pkProver k1 k2 #i #j. Generatekey(pkOwner,pkProver,k1)@i  & Generatekey(pkOwner,pkProver,k2)@j==> (#i = #j & k1 = k2)"

lemma executability:
  exists-trace
  " Ex #i #j #k. Step3()@i 
  & Step2()@j 
  & Step1()@k
    & #k < #j  
    & #j < #i 
    & not (Ex X  #r. Revltk(X)@r)
     & not (Ex Owner  Prover  #r. Revk(Owner,Prover)@r)
    "

lemma Aliveness_Owner_ProverI:
  "
      All Owner Prover Ti  #i.
        Commit(Owner, Prover, <'Ti',Ti>) @ i
      ==>
          (Ex Owner1 t1  #j. Running(Prover, Owner1, <'Ti',t1>) @ j & j < i
          )
        | (Ex #r. Revltk(Prover)  @ r)
        |  (Ex Owner  Prover  #r. Revk(Owner,Prover)@r)
  "
lemma Weak_agree_Owner_ProverI:
  "
      All Owner Prover Ti  #i.
        Commit(Owner, Prover, <'Ti',Ti>) @ i
      ==>
          (Ex t1  #j. Running(Prover, Owner, <'Ti',t1>) @ j & j < i
          )
        | (Ex #r. Revltk(Prover)  @ r)
        |  (Ex Owner  Prover  #r. Revk(Owner,Prover)@r)
  "
lemma Noninjective_agree_Ti_Owner_ProverI:
  "
      All Owner Prover Ti  #i.
        Commit(Owner, Prover, <'Ti',Ti>) @ i
      ==>
          (Ex #j. Running(Prover, Owner, <'Ti',Ti>) @ j & j < i
          )
        | (Ex #r. Revltk(Prover)  @ r)
        |  (Ex Owner  Prover  #r. Revk(Owner,Prover)@r)
  "




lemma Noninjective_agree_repi_Owner_ProverI:
  "
      All Owner Prover  repi #i.
        Commit(Owner, Prover, <'repi',repi>) @ i
      ==>
          (Ex #j. Running(Prover, Owner, <'repi',repi>) @ j & j < i
          )
        | (Ex #r. Revltk(Prover)  @ r)
        |  (Ex Owner  Prover  #r. Revk(Owner,Prover)@r)
  "
lemma Noninjective_agree_pidi_Owner_ProverI:
  "
      All Owner Prover  pid #i.
        Commit(Owner, Prover, <'pidi',pid>) @ i
      ==>
          (Ex #j. Running(Prover, Owner, <'pidi',pid>) @ j & j < i
          )
        | (Ex #r. Revltk(Prover)  @ r)
        |  (Ex Owner  Prover  #r. Revk(Owner,Prover)@r)
  "

lemma Injective_agree_Ti_Owner_ProverI:
  "
      All Owner Prover Ti  #i.
        Commit(Owner, Prover, <'Ti',Ti>) @ i
      ==>
          (Ex #j. Running(Prover, Owner, <'Ti',Ti>) @ j & j < i
          & not (Ex Owner2 Prover2 #i2. Commit(Owner2,Prover2,<'Ti', Ti>)@i2
                            & not (#i2 = #i))
          )
        | (Ex #r. Revltk(Prover)  @ r)
        |  (Ex Owner  Prover  #r. Revk(Owner,Prover)@r)
  "

end
